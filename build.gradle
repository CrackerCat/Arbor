//file:noinspection GroovyAssignabilityCheck
import org.jetbrains.kotlin.gradle.targets.js.nodejs.NodeJsRootExtension

buildscript {
    ext.kotlin_version = '1.8.20'
    ext.dokka_version = '1.4.20'
    ext.jacoco_version = '0.8.8'
    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.19.0"
    id 'nl.fabianm.kotlin.plugin.generated' version '1.3.2'
    id 'org.jetbrains.kotlin.js' version "$kotlin_version"
}

repositories {
    google()
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
}

def getGitCommitCount() {
    try {
        def process = "git rev-list HEAD --count".execute()
        return process.text.trim()
    } catch (Exception e) {
        logger.log(LogLevel.ERROR, "Failed to get Git commit count, is Git installed?", e)
        return null
    }
}

subprojects {
    final isCI = !"$System.env.CI".isEmpty()
    final isMaster = "$System.env.CIRCLE_BRANCH" == "master"
    final buildNumber = getGitCommitCount() ?: "0"

    group 'com.ToxicBakery.logging'
    version "1.40.$buildNumber" + (isCI && isMaster ? "" : "-SNAPSHOT")

    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
}
