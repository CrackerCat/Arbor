buildscript {
    ext.kotlin_version = '1.4.30'
    ext.dokka_version = '1.4.20'
    ext.jacoco_version = '0.8.5'
    repositories {
        mavenLocal()
        google()
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
        maven { url "https://dl.bintray.com/salomonbrys/gradle-plugins" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-frontend-plugin:0.0.45"
        classpath "com.github.salomonbrys.gradle.kotlin.js:kotlin-js-gradle-utils:1.0.0"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.1.1"
    id 'nl.fabianm.kotlin.plugin.generated' version '1.3.2'
}

def getGitCommitCount() {
    try {
        def process = "git rev-list HEAD --count".execute()
        return process.text.trim()
    } catch (Exception e) {
        logger.log(LogLevel.ERROR, "Failed to get Git commit count, is Git installed?", e)
        return null
    }
}

subprojects {
    final isCI = !"$System.env.CI".isEmpty()
    final isMaster = "$System.env.CIRCLE_BRANCH" == "master"
    final buildNumber = getGitCommitCount() ?: "0"

    group 'com.ToxicBakery.logging'
    version "1.35.$buildNumber" + (isCI && isMaster ? "" : "-SNAPSHOT")
    println("Configuring $name - $version")

    repositories {
        google()
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://dl.bintray.com/kotlin/kotlin-dev" }
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
        maven { url "https://dl.bintray.com/salomonbrys/gradle-plugins" }
    }
}
