plugins {
    id 'com.android.library'
    id 'kotlin-multiplatform'
    id 'maven-publish'
    id 'com.github.salomonbrys.gradle.kotlin.js.mpp-tests.node'
//    id 'maven'
    id 'signing'
    id 'org.jetbrains.dokka'
}

archivesBaseName = 'arbor'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 15
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

kotlin {
    targets {
        fromPreset(presets.android, 'android') {
            mavenPublication {
                artifactId = 'arbor-android'
            }
        }
        fromPreset(presets.jvm, 'jvm') {
            mavenPublication {
                artifactId = 'arbor-jvm'
            }
        }
        fromPreset(presets.js, 'js') {
            tasks[compilations.main.compileKotlinTaskName].kotlinOptions.moduleKind = "umd"
            kotlinJsNodeTests {
                thisTarget(js)
            }
            mavenPublication {
                artifactId = 'arbor-js'
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                api "org.jetbrains.kotlin:kotlin-stdlib-common"
            }
        }
        commonTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-common"
                implementation "org.jetbrains.kotlin:kotlin-test-annotations-common"
            }
        }
        jsMain {
            dependencies {
                api "org.jetbrains.kotlin:kotlin-stdlib-js"
            }
        }
        jsTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
            }
        }
        jvmMain {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib'
            }
        }
        jvmTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
            }
        }
        androidMain {
            dependsOn jvmMain
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib'
            }
        }
        androidTest {
            dependsOn jvmTest
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
            }
        }
    }
}

task dokkaAndroid(type: org.jetbrains.dokka.gradle.DokkaTask) {
    impliedPlatforms = ["Android", "Common"]
    kotlinTasks { [] }
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc/android"
    sourceRoot {
        path = kotlin.sourceSets.commonMain.kotlin.srcDirs[0]
        platforms = ["Common"]
    }
    sourceRoot {
        path = kotlin.sourceSets.androidMain.kotlin.srcDirs[0]
        platforms = ["Android"]
    }
}

task dokkaCommon(type: org.jetbrains.dokka.gradle.DokkaTask) {
    impliedPlatforms = ["Common"]
    kotlinTasks { [] }
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc/common"
    sourceRoot {
        path = kotlin.sourceSets.commonMain.kotlin.srcDirs[0]
        platforms = ["Common"]
    }
}

task dokkaJs(type: org.jetbrains.dokka.gradle.DokkaTask) {
    impliedPlatforms = ["JS", "Common"]
    kotlinTasks { [] }
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc/js"
    sourceRoot {
        path = kotlin.sourceSets.commonMain.kotlin.srcDirs[0]
        platforms = ["Common"]
    }
    sourceRoot {
        path = kotlin.sourceSets.jsMain.kotlin.srcDirs[0]
        platforms = ["JS"]
    }
}

task dokkaJvm(type: org.jetbrains.dokka.gradle.DokkaTask) {
    impliedPlatforms = ["JVM", "Common"]
    kotlinTasks { [] }
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc/jvm"
    sourceRoot {
        path = kotlin.sourceSets.commonMain.kotlin.srcDirs[0]
        platforms = ["Common"]
    }
    sourceRoot {
        path = kotlin.sourceSets.jvmMain.kotlin.srcDirs[0]
        platforms = ["JVM"]
    }
}

task emptySourcesJar(type: Jar) {
    classifier = 'sources'
}
task emptyJavadocJar(type: Jar) {
    classifier = 'javadoc'
}

task dokkaJavadocAndroidJar(type: Jar, dependsOn: dokkaAndroid) {
    classifier = 'javadoc'
    from "$buildDir/javadoc/android"
}

task dokkaJavadocCommonJar(type: Jar, dependsOn: dokkaCommon) {
    classifier = 'javadoc'
    from "$buildDir/javadoc/common"
}

task dokkaJavadocJsJar(type: Jar, dependsOn: dokkaJs) {
    classifier = 'javadoc'
    from "$buildDir/javadoc/js"
}

task dokkaJavadocJvmJar(type: Jar, dependsOn: dokkaJvm) {
    classifier = 'javadoc'
    from "$buildDir/javadoc/jvm"
}

publishing {
    publications.all {
        pom.withXml {
            def root = asNode()

            root.children().last() + {
                resolveStrategy = DELEGATE_FIRST

                description POM_DESCRIPTION
                name POM_NAME
                url POM_URL
                scm {
                    url POM_SCM_URL
                    connection POM_SCM_CONNECTION
                    developerConnection POM_SCM_DEV_CONNECTION
                }
                licenses {
                    license {
                        name POM_LICENCE_NAME
                        url POM_LICENCE_URL
                        distribution POM_LICENCE_DIST
                    }
                }
                developers {
                    developer {
                        id POM_DEVELOPER_ID
                        name POM_DEVELOPER_NAME
                        email POM_DEVELOPER_EMAIL
                        organization POM_DEVELOPER_ORGANIZATION
                        organizationUrl POM_DEVELOPER_ORGANIZATION_URL
                    }
                }
            }
        }
    }

    afterEvaluate { project ->
        if (project.getPlugins().hasPlugin('com.android.library')) {
            task androidSourcesJar(type: Jar) {
                classifier = 'sources'
                // Android source are missing the common sources so they need to be mashed together
                from kotlin.sourceSets.androidMain.kotlin.srcDirs.plus(kotlin.sourceSets.commonMain.kotlin.srcDirs)
            }
        }

        publications.getByName('android') {
            artifacts.clear()
            artifact("$buildDir/outputs/aar/arbor-release.aar")
            artifact dokkaJavadocAndroidJar
            artifact androidSourcesJar
        }
        publications.getByName('js') {
            artifact dokkaJavadocJsJar
        }
        publications.getByName('jvm') {
            artifact dokkaJavadocJvmJar
        }
        publications.getByName('kotlinMultiplatform') {
            artifact dokkaJavadocCommonJar
            artifact emptySourcesJar
        }
        publications.getByName('metadata') {
            artifact dokkaJavadocCommonJar
        }
    }

    repositories {
        def releaseUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
        def snapshotUrl = "https://oss.sonatype.org/content/repositories/snapshots"

        maven {
            url !version.contains("SNAPSHOT") ? releaseUrl : snapshotUrl
            credentials {
                username System.getenv('SONATYPE_USERNAME') ?: ""
                password System.getenv('SONATYPE_PASSWORD') ?: ""
            }
        }
        maven {
            name = 'test'
            url "file://${rootProject.buildDir}/localMaven"
        }
    }
}

signing {
    required false
    sign publishing.publications
}

publish.dependsOn assemble
publishToMavenLocal.dependsOn assemble
